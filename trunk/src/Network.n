/*

*/

using Nemerle.Collections; // for Hashtable

using System;
using System.Net;
using System.IO;
using System.Guid;

using System.Runtime.Remoting;
using System.Runtime.Remoting.Channels;
using System.Runtime.Remoting.Channels.Tcp;
using System.Runtime.Remoting.Messaging;
using System.Threading;

using Chalks; 
using Chalks.ConcurrentEdition;
using Chalks.Gui;
using Chalks.Gui.ChalksWindow; // give access to the log


namespace Chalks.Network
{


    // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
    class AuthenticationRealm :MarshalByRefObject
    { // object that allow login of remote users
      // all his public methods are exposed to remote calls

        local_node :NetworkNode;
        
        public this(_local_node :NetworkNode)
        {
            local_node = _local_node; // this object is private

            // just for debugging
            log($"AuthenticationRealm instance created $(this.GetHashCode())"); 
            ;
        }
        
        public login(site_id :Guid,
                     nickname :string, password :string
                     ) :ConcurrentEditableNodeProxy * Guid * string
        {
            when (local_node.remote_sites.Contains(site_id))
                throw RemotingException("Fatal error: overlapping site_ids");

            when (nickname.Length < 3)
                throw RemotingException(
                $"Nickname has to have at least three characters. Yours was '$(nickname)'.");

            when ( ! local_node.is_equal_to_password(password) )
                throw RemotingException("Password does not match");
            
            
            log(1, $"Site $(site_id) named as '$(nickname)' was accepted in the realm");
            
            // give access to the local node, send us identifiers
            (ConcurrentEditableNodeProxy(site_id, local_node),
             local_node.get_site_id(), local_node.nickname);
        }


        // Serialized tuple bug workaround
        public login(site_id :Guid,
                     nickname :string, password :string,
                     ret_node_proxy :out ConcurrentEditableNodeProxy,
                     ret_site_id  :out Guid,
                     ret_nickname :out string) :void
        {
            (ret_node_proxy, ret_site_id, ret_nickname) = login(site_id, nickname, password);
        }
        
    } // end of AuthenticationRealm class
        
    // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
        
    public class ConcurrentEditableNodeProxy :MarshalByRefObject
    // manage the network calls to the local calls
    // all his public methods are exposed to remote calls
    // this is the object that is passed to other nodes when login
    {

        local_node :NetworkNode;
        remote_site_id :Guid;
        
        public this(_site_id :Guid, _local_node :NetworkNode)
        {
            local_node = _local_node; // this object is private
            // store the id of the remote node that uses the reference to this proxy instance
            remote_site_id = _site_id; 
            ;
        }


        public add_node(remote_node :ConcurrentEditableNodeProxy) :ConcurrentEditableNodeStatus
        { // a remote node connects himself
          // return the "Set text" operation and send all the HB  

          // register the new remote_node_proxy reference
          local_node.remote_sites[remote_site_id] = remote_node;

          local_node.cce.get_status();
        }
        
        
        public receive_operation(op :Operation) :void
        {
            // receive
            local_node.cce.receive_operation(op);
            // and relay
            local_node.send_operation(remote_site_id, op);
        }

        public receive_message(sender :Guid, addressee :Guid, message :string) :void
        {
            // receive
            // if it is a broadcast or we are the addressee
            when ( addressee == Guid.Empty || addressee == local_node.get_site_id() ) {
                local_node.gui.receive_message(sender, message);
            }
            
            // and relay
            when (addressee != local_node.get_site_id() ) {
                local_node.send_message(remote_site_id, sender, addressee, message);
            }
        }

        public get_site_id() :Guid
        {
            local_node.get_site_id();
        }

        public get_nickname() :string
        {
            "get_nickname is not yet implemented";
        }

    }

    // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
            
    public class NetworkNode :INetworkForCe
    {
        static default_service_name = "chalks";
        min_port = 1080;
        max_port = 1090;
        
        site_id :Guid = System.Guid.NewGuid();

        public gui :IGuiForNetwork;
        public cce :ICeForNetwork;
        
        public mutable nickname :string = "";
        mutable password :string = "";

        authentication_realm :AuthenticationRealm;
        service_name :string;
        mutable service_channel :Runtime.Remoting.Channels.Tcp.TcpServerChannel;
        mutable self_uri :string;

        // site_id => ConcurrentEditableNodeProxy references (give access to other sites)
        public remote_sites :Hashtable[Guid, ConcurrentEditableNodeProxy] = Hashtable();
        // site_id => ConcurrentEditableNodeProxy instances (give access from other sites)
        public local_proxies :Hashtable[Guid, ConcurrentEditableNodeProxy] = Hashtable();
        // site_id => nickname
        public sites_nicknames :Hashtable[Guid, string] = Hashtable(); 

        
        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
        
        #region Constructors and destructor
        
        public this(_gui :IGuiForNetwork, _cce :ICeForNetwork)
        {
            this(_gui, _cce, default_service_name);
            // service_name management is a trick for tests,
            // it should not be used in the application
        }
                
        public this(_gui :IGuiForNetwork, _cce :ICeForNetwork, _service_name :string) 
        {
            
            gui = _gui; // store the gui reference
            gui.set_site_id(site_id); // configure the cce_widget

            cce = _cce; // store concurrent editable reference

            service_name = _service_name;
            authentication_realm = AuthenticationRealm(this);

            start_collaboration_service();
            
            // register ourself with the cce object (we give him network access)
            cce.set_network_interface(this);

            log($"Chalks service aviable at:\n$(self_uri)\n");
        }

        
        protected override Finalize() :void
        {
            Console.WriteLine($"Finalizing networknode $this");
            stop_collaboration_service();
        }

        #endregion
        
        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~

        #region Start/Stop Collaboration service
        
        public start_collaboration_service() :void
        {
            // search channel for the service
            mutable found_port = false;
            mutable port = min_port;

            while( (!found_port) && port <= max_port)
            {
                try {
                    service_channel = Runtime.Remoting.Channels.Tcp.TcpServerChannel("", port);
                    ChannelServices.RegisterChannel (service_channel);
                    found_port = true;
                }
                catch {
                    e => match(e)
                    {
                        | _ is Net.Sockets.SocketException
                        // raised by occupied ports

                        //| _ is System.Runtime.Remoting.RemotingException
                        // raised by already occupied uri

                        =>
                          ChannelServices.UnregisterChannel (service_channel);
                          port +=1;
                          //Console.WriteLine($"Going to tests port $(port.ToString())"); 
                          //Console.Out.Flush();
                          
                        | e => throw e; // other exception are not managed
                    }
                }
            }
              

            assert(port < max_port,
                   $"Could not find an aviable port in the range $(min_port)-> $(max_port)");

            Console.WriteLine($"Found available port $(port)"); Console.Out.Flush();
            
            // Publish the authentification service
            _ = RemotingServices.Marshal (authentication_realm,
                                          service_name, typeof(AuthenticationRealm));
       
            // set the uri of the published object
            def self_ip = find_self_ip();
            self_uri = $"tcp://$(self_ip):$(port)/$(service_name)";
            
            log($"Chalks service aviable at:\n$(self_uri)\n");
        }

        public stop_collaboration_service() :void
        {

            Console.WriteLine( $"Stoping collaboration service at $self_uri");
            
            _ = RemotingServices.Disconnect (authentication_realm); // free the uri
            ChannelServices.UnregisterChannel (service_channel); // free the port
        }

        #endregion

        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
        
        
        public is_equal_to_password(credential :string) :bool
        {
            credential == password;
        }
        
        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
        
        # region Methods used by the Gui

        public start_collaborating(uri :string) :bool
        { // this implement the protocol described in the schema at
          // chalks/doc/schemas/chalks.dia
          
            mutable ret = true;
            try {
                log($"Starting collaboration with $(uri)");
                log($"Client : $(this.self_uri) -> Server $(uri)"); // just for debugging
                log("Trying to connect");

                // requesting the realm remote object
                try {
                    // create a client channel
                    ChannelServices.RegisterChannel(TcpClientChannel ()); 
                } catch {
                    | _ is RemotingException => (); // if the channel was already registered
                } 
                
                def remote_authentication_realm =
                    Activator.GetObject( typeof(AuthenticationRealm), uri) :> AuthenticationRealm; 
                log("Starting authentication process");
                // Serialized tuple bug workaround
                /*def ret_tuple = remote_authentication_realm.login(site_id, nickname, password);
                def (parent_node, parent_site_id, parent_nickname) = ret_tuple;*/

               
                mutable parent_node = null :ConcurrentEditableNodeProxy;
                mutable parent_site_id = Guid.Empty;
                mutable parent_nickname = "";
                
                remote_authentication_realm.login(
                                          site_id, nickname, password, // in
                                          out parent_node,    //out
                                          out parent_site_id, //out
                                          out parent_nickname //out
                                                  );
                                
                log("Credentials accepted, retrieving document");
                def self_node_proxy = ConcurrentEditableNodeProxy(parent_site_id, this);
                def parent_status = parent_node.add_node(self_node_proxy);

                // store the references
                remote_sites[parent_site_id] = parent_node;
                local_proxies[parent_site_id] = self_node_proxy;
                sites_nicknames[parent_site_id] = parent_nickname;

                log("Starting collaboration");
                cce.set_status(parent_status);
            }
            catch {
                | e =>
                  ret = false;
                  log($"Connection failed : \n$e\n\n");
                  when (Chalks.Gui.ChalksWindow.verbosity_level > 0)
                        throw e; // just for debugging
            }

            when(ret) log($"Successful connection with  $(uri)\n");
            
            ret;
        } // end of start_collaborating
        

        public delegate receive_message_delegate(_ :Guid, _ :Guid, _ :string) :void;
        
        public send_message(comes_from :Guid,
                            sender :Guid, addressee :Guid, message :string) :void 
        { // sends a message to another user
            // when site_id == Guid.Empty, the message is a broadcast

            def async_callback(async_result :IAsyncResult) :void 
            { // local definition of the remote method callback
                def _delegate = ((async_result :> AsyncResult).AsyncDelegate) :>receive_message_delegate;
                // receive_message returns void, so, we only show success
                log($"Message sent to $(addressee.ToString()) was received");
                //+ _delegate.EndInvoke(async_result).ToString() );
            }

            
            def send_a_message(site :ConcurrentEditableNodeProxy,
                               sender :Guid, addressee :Guid, message :string) :void
            { // local helper function that send a message to a remote instance
              // the action is asyncronous
                
                def remote_method_delegate = receive_message_delegate(site.receive_message);
                _ = remote_method_delegate.BeginInvoke(sender, addressee, message,
                                                       AsyncCallback(async_callback), null);
            }
            
            if (   addressee != Guid.Empty //addressed message
                && remote_sites.ContainsKey(addressee)) { // and we know the addressee
                    def site = remote_sites[addressee];
                    send_a_message(site, sender, addressee, message);
            } // if we do not know the adressee, we broadcast the message
            
            else { // broadcast message
                foreach((remote_site_id, site) in remote_sites.KeyValuePairs) {
                    when(remote_site_id != comes_from) {
                        send_a_message(site, sender, addressee, message); }}
            }
            ; // return void
        }

        
        public get_self_uri() :string
        {// get the uri of the published object
            self_uri;
        }

        public get_site_nickname(site_id :Guid) :string
        { // return the nickname of a remote user
            sites_nicknames[site_id];
        }

        public discover_nodes() :void
        { // return a list of published documents->uri
          // this method use RendezVous services 

            ;
        }

        public set_nickname(_nickname :string) :void
        {
            nickname = _nickname;
            // this event should be propagated to the remote nodes
        }
        
        public set_password(_password :string) :void
        { // set the access password for the local document
          // it is also the password used to connect to other nodes
            // if users are already connected to the local node, disconnect them, they need a new credential
            ;
        }
        #endregion

        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~
        
        #region Methods used by the ConcurrentEditable object

        public delegate receive_operation_delegate(_ :Operation) :void;
        
        public send_operation(comes_from :Guid, op :Operation) :void
        { // broadcast an operation to the other sites
            
            foreach((remote_site_id,site) in remote_sites.KeyValuePairs) {
                when(remote_site_id != comes_from) {
                    def remote_method_delegate =
                               receive_operation_delegate(site.receive_operation);
                    _ =  remote_method_delegate.BeginInvoke(op, null, null);
                                                            //AsyncCallback(null), null);
                }
            }
            ; // return void
        }

        public get_site_id() : Guid
        { // return the unique site id for the local document
            site_id;
        }

        #endregion

        // -~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~-~

        #region Helpers methods

        find_self_ip() :string
        { // get the best estimate of our network ip
            
            mutable ip="127.0.0.1";
            
            try {
                def url = "http://imgseek.sourceforge.net/cgi-bin/getMyAddress.pl";
                def http_web_request = WebRequest.Create(url);
                http_web_request.Method = "GET";
                def http_web_response = http_web_request.GetResponse();
                def stream = StreamReader(http_web_response.GetResponseStream());
                ip = stream.ReadToEnd();
                stream.Close();
            }
            catch { // could not do the webpage trick
                | _ =>
                    def host = Net.Dns.GetHostByName(Net.Dns.GetHostName());
                    ip = Net.IPAddress(host.AddressList[0].Address).ToString();      
            }
    
            ip;
        }
               
        #endregion
        
    } // end of class NetworkNode

    public interface INetworkForCe
    { // this are the methods that the ConcurrentEdition layer use of the NetworkNode object
        get_site_id() :Guid;
        send_operation(comes_from :Guid, op:Operation) :void;
    }

}